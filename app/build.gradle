buildscript {
    ext {
        springBootVersion = '2.0.5.RELEASE'
    }
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:1.5.2"
    }
}

plugins {
    id 'org.springframework.boot' version '2.0.5.RELEASE'
}

apply plugin: 'io.spring.dependency-management'
apply plugin: 'info.solidsoft.pitest'
apply plugin: "com.gorylenko.gradle-git-properties"

// No need to upload this JAR, it is not a dependency to any other
// Also, the repo does not allow files larger than 32MB
uploadArchives.enabled = false

jar {
    baseName = 'app'
}

processResources {
    filesMatching('**/*.properties') { expand(project.properties) }
//    expand(project.properties)
}

configurations {
    compile.exclude module: "spring-boot-starter-tomcat"
}

project.ext {
    builddate = new Date()
    buildnumber = "$System.env.BUILD_NUMBER"
}

assemble.doLast {
    copy {
        from file("$buildDir/libs/app-${version}.jar")
        into file("$buildDir/docker")
    }
    
}

//springBoot {
//    buildInfo()
//}

pitest {
    targetClasses = ['se.cygni.paintbot.*']
    threads = 4
    outputFormats = ['XML', 'HTML']
}

dependencies {
    compile project(':domain')
    compile project(':api')
    compile project(':client-util')

    compile("ch.qos.logback:logback-classic:$logbackVersion")
    compile("org.slf4j:slf4j-api:$slf4jVersion")
    compile("org.slf4j:jcl-over-slf4j:$slf4jVersion")
    compile("org.slf4j:log4j-over-slf4j:$slf4jVersion")
    compile("org.slf4j:jul-to-slf4j:$slf4jVersion")

    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.boot:spring-boot-starter-jersey')
    compile('org.springframework.boot:spring-boot-starter-websocket')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-jetty')
    compile('org.elasticsearch:elasticsearch:6.8.22')
    compile('org.elasticsearch.client:elasticsearch-rest-high-level-client:6.8.22')

    compile("com.google.guava:guava:$guavaVersion")
    compile("commons-beanutils:commons-beanutils:$commonsBeanutilsVersion")

    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.springframework.restdocs:spring-restdocs-mockmvc')
    testCompile('org.mockito:mockito-core:2.23.0')
    testCompile("org.assertj:assertj-core:3.11.1")
}