apply plugin: 'com.github.ben-manes.versions'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.3.0'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.20.0'
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6.2"
    }
}

allprojects {

    group = 'se.cygni.paintbot'
    version = '0.1.23'

    /* Enable this for compiler warnings
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
    */

    task downloadDependencies() {
        description 'Download all dependencies to the Gradle cache'
        doLast {
            configurations.findAll { it.canBeResolved }.files
        }
    }
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'jacoco'
    //apply plugin: 'org.sonarqube'
    apply plugin: 'maven-publish'
    apply plugin: 'maven'

    jacoco {
        toolVersion = "0.8.2"
    }

    compileJava {
        sourceCompatibility = 11
        targetCompatibility = 11
        options.encoding = 'UTF-8'
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: "https://snakebot-maven-repo.appspot.com") {
                    authentication(userName: "${System.getenv('mvn_user')}", password: "${System.getenv('mvn_pwd')}")
                }
            }
        }
    }

    repositories {
        mavenCentral()
    }
    ext['slf4j.version'] = '1.7.25'
    ext {
        slf4jVersion = '1.7.25'
        logbackVersion = '1.2.3'
        jacksonVersion = '2.9.7'
        springBootVersion = '2.0.5.RELEASE'
        springVersion = '5.1.0.RELEASE'
        springDataMongoVersion = '2.1.0.RELEASE'
        commonsMathVersion = '3.6.1'
        commonsLangVersion = '3.8.1'
        commonsBeanutilsVersion = '1.9.3'
        commonsCollectionsVersion = '4.2'
        guavaVersion = '26.0-jre'
    }

    task javadocJar (type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }
    task sourceJar (type : Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourceJar
        archives javadocJar
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId 'paintbot-' + project.name
                from components.java
                artifact (sourceJar) {
                    classifier = 'sources'
                }
                artifact (javadocJar) {
                    classifier = 'javadoc'
                }
            }
        }
    }
    publishing {
        repositories {
            maven {
                // This is for jenkins local filesystem
                url "/var/www/mavenrepo"
            }
        }
    }

    dependencies {
        testCompile "junit:junit:4.12"
        testCompile 'org.hamcrest:hamcrest-library:1.3'
    }

    test {
        jacoco {
            append = false
        }
    }
}

